## Разделение текста на лексемы

Текст рассматривается как последовательность лексем. Лексема - это слово или разделитель слов. Слово - это непрерывная последовательность букв и цифр. Разделитель слов - это знак препинания, специальный символ, разделитель строк или пробел. Пробелом считается непрерывная последовательность символов пробела, сиволов табуляции и символов без начертания.

## Имена

`Имя-1` — придуманное имя, используемое для обозначения лексем, шаблонов, полей шаблонов или пространств имён шаблонов. Имена чувствительны к прописным и строчным буквам, должны начинаться с буквы и могут включать буквы, цифры и дефисы. Символ подчёркивания не допускается (имя языка Невод можно преобразовать в идентификатор любого популярного языка программирования, заменив дефисы символами подчёркивания).

`Моя.Область` — составное имя, включает точку в качестве разделителя частей. Составные имена можно использовать для шаблонов и пространств, но не полей шаблонов.

`Моя.Область.Шаблон-1` — полное составное имя некоторого шаблона.

`Шаблон-1` — основной шаблон. `Шаблон-1.Подшаблон` — подчинённый шаблон. Шаблоны с составными именами считаются вложенными шаблонами, если их начальная часть совпадает с именем какого-либо другого шаблона, который считается основным шаблоном. Основной шаблон может ссылаться на вложенные шаблоны, опуская начальную часть.

## Ключевые слова

Все ключевые слова начинаются с символа `@`, поэтому в языке нет зарезервированных слов, а имена, совпадающие с обычными словами, можно использовать для определения шаблонов. Следующие слова считаются ключевыми:

`@требуется` — ключевое слово для использования требуемого пакета шаблонов.

`@пространство` — ключевое слово для определения пространства имён шаблонов.

`@шаблон` — ключевое слово для определения шаблона.

`@искать` — ключевое слово для обозначения шаблона в качестве цели поиска.

`@где` — ключевое слово для определения вложенных шаблонов.

`@внутри` — ключевое слово, используемое в качестве оператора в выражениях.

`@вне` — ключевое слово, используемое в качестве оператора в выражениях.

`@содержащий` — ключевое слово, используемое в качестве оператора в выражениях.

## Образцы текста

`"текст"` — текст, сравниваемый без учёта прописных и строчных букв.

`"текст"!` — текст, сравниваемый с учётом прописных и строчных букв.

`'текст'` — текст, сравниваемый без учёта прописных и строчных букв. Может содержать двойные кавычки.

`'текст'!` — текст, сравниваемый с учётом прописных и строчных букв. Может содержать двойные кавычки.

`'текст ''Невод'' в кавычках'` — текст, сравниваемый без учёта прописных и строчных букв. Две кавычки делают кавычку частью текста.

`"текст ""Невод"" в двойных кавычках"` — текст, сравниваемый без учёта прописных и строчных букв. Две двойные кавычки делают двойную кавычку частью текста.

`'текст'*` — текст с началом слова, сравниваемым без учёта прописных и строчных букв.

`'текст'!*` — текст с началом слова, сравниваемым с учётом прописных и строчных букв.

`'текст'*(3-6)` — текст с началом слова, сравниваемым без учёта прописных и строчных букв, и с длиной окончания от трёх до шести букв или цифр.

`'текст'!*(Цифры, 2)` — текст с началом слова, сравниваемым с учётом прописных и строчных букв, и с окончанием, состоящим из двух цифр.

`'текст'*(Буквы, 3-6, Строчные)` — текст с началом слова, сравниваемым без учёта прописных и строчных букв, и с окончанием, состоящим только из строчных букв и длиной от трёх до шести букв.

## Основные лексемы

`Слово` — любое слово, непрерывная последовательность букв алфавита (любого языка) и цифр.

`ЗнакПрепинания` — знак препинания, используемый в письменности для разделения текста на фразы, словосочетания, предложения, для выделения смысловых отрезков и других вспомогательных функций. Следующие символы считаются знаками препинания: `.`, `,`, `!`, `?`, `(`, `)`, `-`, `;`, `:`, `'`, `"`.

`Символ` — неалфавитный символ, например, один из следующих: `@`, `_`, `#`, `$`, `%`, `&`, `*`, `^`, `+`, `=`, `~`, `[`, `]`, `{`, `}`, `<`, `>`, `/`, `\`, `|`.

`Пробел` — непрерывная последовательность символов пробела, символов табуляции и символов без начертания.

`РазделительСтрок` — разделитель строк, обозначает конец текстовой строки.

`Начало` — начало текста, искусственная лексема.

`Конец` — конец текста, искусственная лексема.

## Дополнительные лексемы

`Буквы` — буквенное `Слово`, непрерывная последовательность только букв алфавита (любого языка).

`Цифры` — цифровое `Слово`, непрерывная последовательность только цифр.

`БуквыЦифры` — буквенно-цифровое `Слово`, непрерывная последовательность букв и цифр, которая начинается с буквы.

`ЦифрыБуквы` — буквенно-цифровое `Слово`, непрерывная последовательность букв и цифр, которая начинается с цифры.

`Пропуск` — пробел или разделитель строк. Эквивалентно списку альтернатив: `{Пробел, РазделительСтрок}`.

`РазделительСлов` — любой разделитель слов, такой как пробел, знак препинания, неалфавитный символ или разделитель строк. Эквивалентно списку альтернатив: `{Пробел, ЗнакПрепинания, Символ, РазделительСтрок}`.

`Любое` — любое слово или разделитель слов, то есть любая лексема за исключением лексем `Начало` и `Конец`. Эквивалентно списку альтернатив: `{Слово, РазделительСлов}`

## Параметризованные лексемы

`Слово(Прописные)` — любое слово прописными буквами.

`Цифры(2-4)` — число длиной от двух до четырёх цифр.

`Буквы(2-10, ПерваяПрописная)` — буквенное `Слово` длиной от двух до десяти букв, с первой прописной и последующими строчными буквами.

`БуквыЦифры(3-6, Строчные)` — буквенно-цифровое `Слово` длиной от трёх до шести строчных букв и цифр.

## Последовательность

`А + Б` — последовательность из двух обязательных элементов в строгом порядке.

`А + Б + В` — последовательность из трех обязательных элементов в строгом порядке.

Количество элементов в последовательности не ограничено.

## Вариативность

`{А, Б}` — вариативность из двух альтернатив: либо `А`, либо `Б`.

`{А, Б, В}` — вариативность из трёх альтернатив: либо `А`, либо `Б`, либо `В`.

`{А}` — вариативность из одной альтернативы, эквивалентно `А`.

`{А, ~Б}` — вариативность из одной альтернативы с исключением: `A`, но не `Б`.

`{А, Б, ~В}` — вариативность из двух альтернатив с одним исключением: либо `А`, либо `Б`, но не `В`.

`{А, ~Б, ~В}` — вариативность из одной альтернативы с двумя исключениями: `А`, но не `Б` и не `В`.

Количество альтернатив у вариативности не ограничено. Количество исключений тоже не ограничено, но должна быть хотя бы одна альтернатива.

## Соединение

`А & Б` — соединение `А` и `Б`, означающее, что за `А` следует `Б` или за `Б` следует `А` на любом расстоянии между `А` и `Б`.

`А & Б & В` — соединение `А`, `Б` и `В` в любом порядке и на любом расстоянии.

Количество элементов в соединении не ограничено.

## Повторение

`[3 А]` — повторение `А` ровно три раза.

`[3-5 А]` — повторение `А` от трёх до пяти раз.

`[1+ А]` — повторение `А` один или более раз, верхняя граница отсутствует.

`[0+ А]` — повторение `А` любое количество раз, включая ноль раз, т.е. повторение `А` необязательно.

`[0-3 А]` — повторение `А` от нуля до трёх раз. Повторение `А` необязательно.

`[0-1 А]` — повторение `А` нуль раз или один раз, означающее, что `А` является необязательным.

Рассматриваются все варианты совпадений, начиная с нижней границы и заканчивая верхней границей. Выбирается наибольшее повторение, не пересекающееся само с собой. Если верхняя граница отсутствует, механизм сопоставления динамически выбирает верхнюю границу, чтобы предотвратить слишком длительный поиск, воспринимаемый как бесконечный.

## Необязательность

`? A` — необязательность `A`, эквивалентно повторению: `[0-1 A]`.

## Последовательность слов

`А _ Б` — последовательность из двух обязательных элементов (обычно слов), между которыми находятся разделители слов. Эквивалентно: `А + [0+ РазделительСлов] + Б`.

`А _ Б _ В` — последовательность из трех обязательных элементов (обычно слов), между которыми находятся разделители слов. Эквивалентно: `А + [0+ РазделительСлов] + Б + [0+ РазделительСлов] + В`.

Количество элементов в последовательности не ограничено.

## Промежуток

`А ... Б` — последовательность, начинающаяся с элемента `А` и заканчивающаяся элементом `Б`, с любыми лексемами в промежутке между `А` и `Б`.

`А .. [0-5] .. Б` — последовательность, начинающаяся с элемента `А` и заканчивающаяся элементом `Б`, с промежутком между `А` и `Б`, содержащим не более пяти слов.

## Включения, исключения и пересечения

`А @внутри Б` — элемент `А` совпадает в пределах `Б`, т.е. `А` находится внутри `Б` в тексте.

`А @вне Б` — элемент `А` совпадает вне (за пределами) `Б` т.е. `А` не пересекается с `Б` в тексте.

`А @содержащий Б` — элемент `А` совпадает и содержит внутри `Б`, т.е. `Б` находится внутри `А` в тексте.

## Скобки

`()` — явно управляет приоритетом операторов, например: `? (A + B)`. В отсутствие скобок оператор необязательности `?` относился бы к `A`, но не к последовательности `A + B`.

## Шаблоны

`Ш = "текст";` — шаблон с именем `Ш` определён через образец текста. Совпадение с образцом текста означает совпадение шаблона `Ш`. Имя `Ш` может использоваться для ссылки на этот шаблон в определении других шаблонов.

`Ш = Буквы;` — шаблон с именем `Ш` определён через слово, состоящее только из букв.

`Ш2 = Ш1; Ш1 = Слово;` — два шаблона: шаблон `Ш2` определён через шаблон `Ш1`, а шаблон `Ш1` определён через `Слово`.

`Ш = А + Б;` — шаблон `Ш` определён через последовательность шаблонов `А` и `Б`.

`Ш = {А, Б};` — шаблон `Ш` определён через вариативность с альтернативами `А` и `Б`.

`Ш = [1+ А];` — шаблон `Ш` определён через повторение шаблона `А`.

`#Ш = "текст";` — шаблон `Ш`, определённый через образец текста, помечен хэш-тегом в качестве цели поиска. Результаты поиска содержат совпадения только с целевыми шаблонами.

`#Ш = А + Б;` — целевой шаблон `Ш` определён через последовательность шаблонов `А` и `Б`. Совпадения с целевым шаблоном `Ш` возвращаются в результатах поиска.

`#Ш = {А, Б};` — целевой шаблон `Ш` определён через вариативность с альтернативами `А` и `Б`. Совпадения с целевым шаблоном `Ш` возвращаются в результатах поиска.

`#Ш = [1+ А];` — целевой шаблон `Ш` определён через повторение шаблона `А`. Совпадения с целевым шаблоном `Ш` возвращаются в результатах поиска.

`@шаблон Ш = Буквы;` — шаблон `Ш` определяется с использованием синтаксиса пакета шаблонов.

`@искать @шаблон Ш = Буквы;` — шаблон `Ш` в пакете помечен как цель поиска.

`@шаблон #Ш = Буквы;` — шаблон `Ш` в пакете помечен хэш-тегом в качестве цели поиска. Эквивалентно: `@искать @шаблон Ш = Буквы;`.

`@шаблон Ш = С @где { @шаблон С = Слово; };` — шаблон `Ш` в пакете определён через вложенный шаблон `С`. На шаблон `С` можно ссылаться из других шаблонов с помощью составного имени `Ш.С`.

`@шаблон #Ш = С + П @где { @шаблон #С = Слово; @шаблон П = Пробел; };` — шаблон `Ш` в пакете, являющийся целью поиска, определен с помощью вложенных шаблонов `С` и `П`, причём шаблон `С` тоже является целью поиска. На шаблоны `С` и `П` можно ссылаться из других шаблонов с помощью составных имён `Ш.С` и `Ш.П` соответственно.

Все шаблоны (целевые и нецелевые) можно использовать для создания других шаблонов, при этом в результаты поиска включаются только совпадения с целевыми шаблонами.

## Поля шаблонов и извлечение текста

`#Ш(П, О) = П: А ... О: Б;` — шаблон `Ш` с двумя полями `П` и `О`. Шаблон определён через последовательность, которая начинается с элемента `А` и заканчивается элементом `Б` (с любыми лексемами в промежутке между `А` и `Б`). Значения полей `П` и `О` извлекаются из текста как совпадения элементов `А` и `Б` соответственно. Для каждого поля допускается только одно извлечение текста.

`#Ш(П) = А .. П .. Б;` — шаблон `Ш` с полем `П`. Шаблон определён через последовательность, которая начинается с элемента `А` и заканчивается элементом `Б`, с промежутком между `А` и `Б`. Значение поля `П` извлекается как текст в промежутке между `А` и `Б`.

`#Ш1(П, О) = Ш2(П: Л, О: Е);` — шаблон `Ш1` с двумя полями `П` и `О`. Шаблон определён через другой шаблон `Ш2`, который в свою очередь имеет поля `Л` и `Е`. Значения полей `П` и `О` извлекаются соответственно из полей `Л` и `Е` шаблона `Ш2`.

`#Ш(П) = П: Слово ... П;` — шаблон `Ш` с полем `П`. Шаблон определён через последовательность, которая начинается с некоторого слова и заканчивается этим же словом. Конец последовательности задан значением поля `П`, которое извлекается из текста в качестве начала последовательности. Для каждого поля допускается только одно извлечение текста. Значение поля должно быть извлечено перед использованием.

`#Ш(~П) = П: Слово ... П;` — шаблон `Ш` с полем `П`, помеченным как внутреннее поле. Значения внутренних полей не возвращаются в результатах поиска, но могут использоваться в выражениях.

`#Ш(П) = [1+ П: Символ + П];` — шаблон `Ш` с полем `П`. Шаблон определён как повторение символа, за которым следует тот же символ (повторение двойного символа). Значение поля может использоваться только в контексте извлечения, например, поле `П` извлекается внутри повторения, поэтому оно может использоваться внутри повторения и не может использоваться вне повторения.

`#Ш(П, О) = {П: ЗнакПрепинания + П, О: Символ + О};` — шаблон `Ш` с полями `П` и `О`. Шаблон определён как вариативность с двумя альтернативами: первая альтернатива — это двойной знак препинания, а вторая альтернатива — двойной символ. Значение поля может быть использовано только в контексте извлечения, например, поле `П` извлекается внутри первой альтернативы, поэтому его можно использовать только внутри первой альтернативы, а поле `О` извлекается внутри второй альтернативы, поэтому его можно использовать только внутри второй альтернативы. Для шаблона `Ш` либо поле `П`, либо поле `О` получает значение в зависимости от того, какая альтернатива совпала.

## Пространства имён шаблонов

`@пространство П { @шаблон Ш = Слово; }` — объявление пространства имён шаблонов с именем `П`. Шаблон `Ш`, определённый внутри блока в фигурных скобках, принадлежит пространству имён `П`, и на него можно ссылаться через полное составное имя `П.Ш`. Шаблоны, определенные вне какого-либо пространства имён, считаются принадлежащими пространству имён по умолчанию с пустым именем. Пространства имён используются в пакетах шаблонов.

`@пространство Основное.ДатаВремя { @шаблон Время = {Время24, Время12}; }` — объявление пространства имён шаблонов с составным именем `Основное.ДатаВремя`. На шаблон `Время` в этом пространстве имён можно ссылаться с помощью полного составного имени `Основное.ДатаВремя.Время`.

`@пространство Основное { @пространство ДатаВремя { @шаблон Время = {Время24, Время12}; } }` — объявление пространства имён шаблонов `ДатаВремя`, вложенного в пространство имён `Основное`. На шаблон `Время` в этом пространстве имён можно ссылаться с помощью полного составного имени `Основное.ДатаВремя.Время`.

## Пакеты шаблонов

Связанные шаблоны можно сохранить в отдельный файл, называемый пакетом шаблонов или просто пакетом. Рекомендуемые расширения файла: `.невод`, `.nevod`, `.np` (сокращение от Nevod Patterns). Имя файла используется для ссылки на пакет из других пакетов.

Ниже приведён пример пакета:

```nevod
@пространство Основное
{
  @искать @шаблон GUID-идентификатор = Слово(8) + [3 '-' + Слово(4)] + '-' + Слово(12);
}
```

В приведённом выше пакете объявлено пространство имён `Основное` с целевым шаблоном `GUID-идентификатор`, который служит для поиска значений GUID-идентификаторов в тексте. Этот пакет можно сохранить в файл `"GUID.невод"` и потом использовать в других пакетах. Ниже приведен пример второго пакета, который использует вышеупомянутый первый пакет:

```nevod
@требуется "GUID.невод";

@пространство Основное
{
  @искать @шаблон GUID-идентификатор-в-скобках = '{' + GUID-идентификатор + '}';
}
```

Второй пакет использует первый пакет из файла `"GUID.невод"` и добавляет шаблон `GUID-идентификатор-в-скобках` в пространство имён `Основное`. Шаблон `GUID-идентификатор-в-скобках` определён через ссылку на шаблон `GUID-идентификатор`. Второй пакет может быть сохранен в файл `"GUID-расширения.невод"` и, в свою очередь, использован другими пакетами.

## Использование пакетов шаблонов

`@требуется "GUID.невод";` — использование пакета шаблонов из файла `"GUID.невод"`.

`@требуется "/GUID.невод";` — использование пакета шаблонов из файла `"GUID.невод"`, находящаегося в корневом каталоге.

`@требуется "C:/GUID.невод";` — использование пакета шаблонов из файла `"GUID.невод"`, находящаегося в корневом каталоге диска `"C:"` в операционной системе Windows.

`@требуется "./GUID.невод";` — использование пакета шаблонов из файла `"GUID.невод"`, находящаегося в том же каталоге, что и пакет, содержащий эту директиву.

`@требуется "../GUID.невод";` — использование пакета шаблонов из файла `"GUID.невод"`, находящаегося в родительском каталоге относительно каталога, в котором находится пакет, содержащий эту директиву.

Подключение требуемых пакетов должно выполняться в начале текста пакета, иными словами, директивы `@требуется` должны предшествовать объявлениям пространств имён и определениям шаблонов.

## Цели поиска

При выполнении поиска с использованием пакета учитываются только шаблоны, помеченные как цели поиска. Совпадения только с целевыми шаблонами включаются в результаты поиска.

Когда пакет использует другие пакеты, то целевые шаблоны в используемых пакетах не рассматриваются как цели поиска для использующего их пакета. Например, поиск шаблонов пакета `"GUID-расширения.невод"` означает поиск только шаблона `GUID-идентификатор-в-скобках`, но не шаблона `GUID-идентификатор` в используемом пакете `"GUID.невод"`.

Чтобы в поиске участвовали шаблоны из используемых пакетов, их следует явно объявить в качестве целей поиска в использующем пакете. Например, следующий пакет использует пакет `"GUID.невод"` с шаблоном `GUID-идентификатор` и добавляет этот шаблон в список целей поиска:

```nevod
@требуется "GUID.невод";

@пространство Основное
{
  @искать @шаблон GUID-идентификатор-в-скобках = '{' + GUID-идентификатор + '}';
}

@искать Основное.GUID-идентификатор;
```

В приведённом выше примере шаблон `Основное.GUID-идентификатор` объявлен в качестве цели поиска.

Шаблоны могут быть помечены как цели поиска отдельно от их определения:

```nevod
@требуется "GUID.невод";

@искать Основное.GUID-идентификатор;
@искать Основное.GUID-идентификатор-в-скобках;

@пространство Основное
{
  @шаблон GUID-идентификатор-в-скобках = '{' + GUID-идентификатор + '}';
}
```

В приведенном выше примере шаблон `GUID-идентификатор-в-скобках` объявлен как цель поиска отдельно от своего определения (которое не содержит хэш-тега или ключевого слова `@искать` перед ключевым словом `@шаблон`). Это означает, что шаблон `GUID-идентификатор-в-скобках` рассматривается как цель поиска в своём пакете, но не рассматривается как цель поиска в использующем пакете, который объявляет цель поиска как пространство имён с групповым символом `*`.

Следующий пример показывает, как выполнить поиск группы целевых шаблонов из заданного пространства имён:

```nevod
@требуется "GUID.невод";

@искать Основное.*;

@пространство Основное
{
  @шаблон GUID-идентификатор-в-скобках = '{' + GUID-идентификатор + '}';
}
```

Объявление `@искать Основное.*;` означает, что все целевые шаблоны в пространстве имен `Основное` становятся целями поиска. Обратите внимание, что это относится только к шаблонам, определённым в текущем пакете, и в пакетах, явно используемых с помощью директивы `@требуется`. Если мы, например, изменим используемый пакет `"GUID.невод"`, подключив к нему другие пакеты с целевыми шаблонами из пространства имён `Основное`, то целевые шаблоны из косвенно используемых пакетов не станут целями поиска.

## Библиотека основных шаблонов

Вместе с технологией Невод предоставляется библиотека основных шаблонов, состоящая в настоящее время из следующих пакетов:

`Basic.np` — пакет, который использует все остальные пакеты с основными шаблонами, объявляя все эти шаблоны целями поиска.

`BankCardNumber/Basic.BankCardNumber.np` — пакет, содержащий шаблоны для поиска номеров банковских карт в тексте.

`Basic.Currency.np` — пакет, содержащий шаблоны для поиска валютных значений в тексте.

`Basic.Date.np` — пакет, содержащий шаблоны для поиска значений дат в тексте.

`Basic.Time.np` — пакет, содержащий шаблоны для поиска значений времени в тексте.

`Basic.DateTime.np` — пакет, содержащий шаблоны для поиска значений дат и времени в тексте.

`Basic.Duration.np` — пакет, содержащий шаблоны для поиска продолжительности дат или времени в тексте.

`Basic.Email.np` — пакет, содержащий шаблоны для поиска почтовых адресов в тексте.

`Basic.GUID.np` — пакет, содержащий шаблоны для поиска значений GUID-идентификаторов в тексте.

`Basic.HashTag.np` — пакет, содержащий шаблоны для поиска хэш-тегов (аналогичных Твиттеру) в тексте.

`Basic.IpAddress.np` — пакет, содержащий шаблоны для поиска IP-адресов в тексте.

`Basic.Number.np` — пакет, содержащий шаблоны для поиска целых и вещественных чисел в тексте.

`Basic.PhoneNumber.np` — пакет, содержащий шаблоны для поиска телефонных номеров в тексте.

`Basic.Url.np` — пакет, содержащий шаблоны для поиска интернет-ссылок в тексте.

## Примеры

Ниже приведён пример из основной библиотеки, демонстрирующий извлечение GUID-идентификаторов, хэш-тегов, целых чисел, вещественных чисел, а также процентных значений в тексте:

```nevod
@пространство Основное
{
  @шаблон #GUID = Слово(8) + [3 '-' + Слово(4)] + '-' + Слово(12);
  @шаблон #Хэштег = '#' + {БуквыЦифры, Буквы, '_'} + [0+ {Слово, '_'}];
  @шаблон #Число = {Целое, Вещественное}
  @где
  {
    @шаблон Целое = ?'-' + Цифры @вне Вещественное;
    @шаблон Вещественное = ?{'+', '-'} + Цифры + ?({'.', ','} + Цифры);
  };
  @шаблон #Процент = Цифры + ?Пробел + {'%', 'процент'*};
}
```

Следующий пакет `"Интернет.невод"` демонстрирует шаблон `Интернет-ссылка`, предназначенный для извлечения из текста интернет-ссылок. Он определён через вложенные шаблоны:

```nevod
@пространство Основное
{
  @искать @шаблон Интернет-ссылка(домен, путь, запрос, якорь) =
    Схема + домен:Домен + ?Порт + ?путь:Путь + ?запрос:Запрос +
    ?якорь:Якорь
  @где
  {
    Схема = {'http', 'https' , 'ftp', 'mailto', 'file', 'data',
      'irc'} + '://';
    Домен = Слово + [1+ '.' + Слово + [0+ {Слово, '_', '-'}]];
    Порт = ':' + Цифры;
    Путь = ?'/' + [0+ {Слово, '/', '_', '+', '-', '%', '.'}];

    Запрос = '?' + ?(Параметр + [0+ '&' + Параметр])
    @где
    {
      Параметр = Идентификатор + '=' + Идентификатор
      @где
      {
        Идентификатор = {Буквы, БуквыЦифры, '_'} + [0+ {Слово, '_'}];
      };
    };

    Якорь(значение) = '#' + значение:Слово;
  };
}
```

Следующий пакет демонстрирует шаблон `Адрес-почты`, предназначенный для извлечения из текста почтовых адресов. Он определён через вложенные шаблоны:

```nevod
@требуется "./Интернет.невод";

@пространство Основное
{
  @искать @шаблон Адрес-почты =
    {Локальное-имя, Локальное-имя-в-кавычках} + '@' + Ссылка.Домен
  @где
  {
    Локальное-имя = Слово + [0+
      {Слово, ЗнакПрепинания, Символ, ~Запрещённые-символы, ~[2+ '.'], ~'.@'}]
    @где
    {
      Запрещённые-символы = {'"', '(', ')', ',', ':', ';', '<', '>',
        '@', '[', '\', ']'};
    };
    Локальное-имя-в-кавычках = '"' ... '"';
  };
}
```
